---
layout: "couchbase-capella"
page_title: "Couchbase Capella Provider 1.4.0: Upgrade and Information Guide"
sidebar_current: "docs-couchbase-capella-guides-140-upgrade-guide"
description: |-
Couchbase Capella Provider 1.4.0: Upgrade and Information Guide
---


# Couchbase Capella Provider 1.4.0: Upgrade and Information Guide

New features in 1.4.0


## New Features
* Enable Flush Bucket [`couchbase-capella_flush`](https://registry.terraform.io/providers/couchbasecloud/couchbase-capella/latest/docs/resources/flush)
* Enable Azure Network Peer [`couchbase-capella_network_peer`](https://registry.terraform.io/providers/couchbasecloud/couchbase-capella/latest/docs/resources/network_peer)


## Flush Bucket
Use the new `flush` resource to flush a bucket
```
output "new_bucket" {
    value = couchbase-capella_bucket.new_bucket
}
```
```
output "bucket_id" {
    value = couchbase-capella_bucket.new_bucket.id
}
```
```
resource "couchbase-capella_bucket" "new_bucket" {
    name                       = var.bucket.name
    organization_id            = var.organization_id
    project_id                 = var.project_id
    cluster_id                 = var.cluster_id
    type                       = var.bucket.type
    storage_backend            = var.bucket.storage_backend
    memory_allocation_in_mb    = var.bucket.memory_allocation_in_mb
    bucket_conflict_resolution = var.bucket.bucket_conflict_resolution
    durability_level           = var.bucket.durability_level
    replicas                   = var.bucket.replicas
    flush                      = var.bucket.flush
    time_to_live_in_seconds    = var.bucket.time_to_live_in_seconds
    eviction_policy            = var.bucket.eviction_policy
}
```
```
output "new_flush" {
    value = couchbase-capella_flush.new_flush
}
```
```
resource "couchbase-capella_flush" "new_flush" {
    organization_id = var.organization_id
    project_id      = var.project_id
    cluster_id      = var.cluster_id
    bucket_id       = var.bucket_id
}
```
For more information, see the [flush bucket examples](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/tree/main/examples/flush_bucket).

## Azure Network Peer

Use the `azure_network_peer_command` data source to get an AWS command for configuring VPC endpoint.
```
output "azure_network_peer_command" {
  value = data.couchbase-capella_azure_network_peer_command.azure_network_peer_command
}
```
```
data "couchbase-capella_azure_network_peer_command" "azure_network_peer_command" {
  organization_id                = var.organization_id
  project_id                     = var.project_id
  cluster_id                     = var.cluster_id
  tenant_id                      = var.tenant_id
  vnet_id                        = var.vnet_id
  subscription_id                = var.subscription_id
  resource_group                 = var.resource_group
  vnet_peering_service_principal = var.vnet_peering_service_principal
}
```
Create a new Azure network peer, use the `network-peer` resource as follows:
```
output "new_network_peer" {
    value = couchbase-capella_network_peer.new_network_peer
}
```
```
output "peer_id" {
    value = couchbase-capella_network_peer.new_network_peer.id
}
```
```
resource "couchbase-capella_network_peer" "new_network_peer" {
    organization_id = var.organization_id
    project_id      = var.project_id
    cluster_id      = var.cluster_id
    name            = var.network_peer.name
    provider_type   = var.network_peer.provider_type
    provider_config = {
        azure_config = {
            tenant_id       = var.azure_config.tenant_id
            resource_group  = var.azure_config.resource_group
            subscription_id = var.azure_config.subscription_id
            cidr            = var.azure_config.cidr
            vnet_id         = var.azure_config.vnet_id
        }
    }
}
```
For more information, see the [network peer examples](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/tree/main/examples/network_peer).

## GSI
To create a deferred index, use the `query_indexes` resource as follows:
```
locals {
  index_template = templatefile("${path.module}/indexes3.json", {
    organization_id = var.organization_id
    project_id      = var.project_id
    cluster_id      = var.cluster_id
  })

  decoded_template = jsondecode(local.index_template)
  index_names      = [for idx, details in local.decoded_template.resource["couchbase-capella_indexes"] : details.index_name]
}
```
```
resource "couchbase-capella_query_indexes" "new_indexes" {
  for_each        = jsondecode(local.index_template).resource["couchbase-capella_indexes"]
  organization_id = each.value.organization_id
  project_id      = each.value.project_id
  cluster_id      = each.value.cluster_id
  bucket_name     = each.value.bucket_name
  scope_name      = each.value.scope_name
  collection_name = each.value.collection_name
  index_name      = each.value.index_name
  index_keys      = each.value.index_keys
  with = {
    defer_build = each.value.with.defer_build
  }
}
```
```
resource "couchbase-capella_query_indexes" "build_idx" {
  organization_id = var.organization_id
  project_id      = var.project_id
  cluster_id      = var.cluster_id

  bucket_name     = var.bucket_name
  scope_name      = var.scope_name
  collection_name = var.collection_name

  build_indexes = local.index_names
}
```
```
output "mon_idx1" {
  value = data.couchbase-capella_query_index_monitor.mon
}
```
```
data "couchbase-capella_query_index_monitor" "mon" {
  organization_id = var.organization_id
  project_id      = var.project_id
  cluster_id      = var.cluster_id
  bucket_name     = var.bucket_name
  scope_name      = var.scope_name
  collection_name = var.collection_name
  indexes         = local.index_names

  depends_on = [couchbase-capella_query_indexes.build_idx]
}
```
For more information, see the [query_indexes](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/tree/main/examples/gsi/deferred).

To create a non-deferred index, use the `query_indexs` resource as follows:
```
locals {
  index_template = templatefile("${path.module}/indexes.json", {
    organization_id = var.organization_id
    project_id      = var.project_id
    cluster_id      = var.cluster_id
  })
}
```
```
resource "couchbase-capella_query_indexes" "new_indexes" {
  for_each        = jsondecode(local.index_template).resource["couchbase-capella_indexes"]
  organization_id = each.value.organization_id
  project_id      = each.value.project_id
  cluster_id      = each.value.cluster_id
  bucket_name     = each.value.bucket_name
  scope_name      = each.value.scope_name
  collection_name = each.value.collection_name
  index_name      = each.value.index_name
  index_keys      = each.value.index_keys
}
```
For more information, see the [query_indexes](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/tree/main/examples/gsi/non_deferred).

### Helpful Links

- [Getting Started with the Terraform Provider](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/blob/master/examples/getting_started)
- [Capella Management API v4.0](https://docs.couchbase.com/cloud/management-api-reference/index.html)
- [See Specific Examples](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/blob/master/examples)