---
layout: "couchbase-capella"
page_title: "Couchbase Capella Provider 1.1.0: Upgrade and Information Guide"
sidebar_current: "docs-couchbase-capella-guides-110-upgrade-guide"
description: |-
Couchbase Capella Provider 1.1.0: Upgrade and Information Guide
---

# Couchbase Capella Provider 1.1.0: Upgrade and Information Guide

Here is a list of what's new in 1.1.0

## New Features

* Create and manage scopes within buckets with [`couchbase-capella_scope`](https://registry.terraform.io/providers/couchbasecloud/couchbase-capella/latest/docs/resources/scope)
* Create and manage multiple collections in a scope with [`couchbase-capella_collection`](https://registry.terraform.io/providers/couchbasecloud/couchbase-capella/latest/docs/resources/collection)
* Schedule an activation schedule to turn on/off clusters and any linked app services based on expected usage using [`couchbase-capella_cluster_onoff_schedule`](https://registry.terraform.io/providers/couchbasecloud/couchbase-capella/latest/docs/resources/cluster_onoff_schedule)
* Turn Clusters on/off on demand with [`couchbase-capella_cluster_onoff_ondemand`](https://registry.terraform.io/providers/couchbasecloud/couchbase-capella/latest/docs/resources/cluster_onoff_ondemand)
* Turn App Services on/off on demand with [`couchbase-capella_app_service_onoff_ondemand`](https://registry.terraform.io/providers/couchbasecloud/couchbase-capella/latest/docs/resources/app_service_onoff_ondemand)
* Import sample datasets into buckets with [`couchbase-capella_sample_bucket`](https://registry.terraform.io/providers/couchbasecloud/couchbase-capella/latest/docs/resources/sample_bucket)
* Enable storage auto-expansion for Azure clusters while creating them via Terraform by adding an `autoexpansion` field to the cluster resource.

## Changes

There are no deprecations as part of this release.

1.1.0 also includes general improvements and bug fixes. See the [CHANGELOG](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/blob/master/CHANGELOG.md) for more specific information.

## Managing Scopes & Collections in Buckets

To create a new scope in a bucket, use the `scope` resource as follows:

```
resource "couchbase-capella_scope" "inventory" {
  scope_name      = "inventory"
  organization_id = var.organization_id
  project_id      = var.project_id
  cluster_id      = var.cluster_id
  bucket_id       = var.bucket_id
}
```

Use the `scopes` datasource to fetch existing list of scopes for the selected bucket.

```
output "scopes_list" {
  value = data.couchbase-capella_scopes.existing_scopes
}

data "couchbase-capella_scopes" "existing_scopes" {
  organization_id = var.organization_id
  project_id      = var.project_id
  cluster_id      = var.cluster_id
  bucket_id       = var.bucket_id
}
```

For more information, see the [examples to manage scopes](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/blob/master/examples/bucket/scopes)


To create a collection inside this scope, use the `collection` resource:

```
resource "couchbase-capella_collection" "books" {
  organization_id = var.organization_id
  project_id      = var.project_id
  cluster_id      = var.cluster_id
  bucket_id       = var.bucket_id
  scope_name      = couchbase-capella_scope.inventory.scope_name
  collection_name = "books"
  max_ttl         = var.collection.max_ttl
}
```

Use the `collections` datasource to fetch existing list of collections in the selected scope and bucket.

```
output "collections_list" {
  value = data.couchbase-capella_collections.existing_collections
}

data "couchbase-capella_collections" "existing_collections" {
  organization_id = var.organization_id
  project_id      = var.project_id
  cluster_id      = var.cluster_id
  bucket_id       = var.bucket_id
  scope_name      = var.scope_name
}
```

For more details, see the example for [managing collections](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/blob/master/examples/bucket/collections)

For an end-to-end example on creating bucket, scope and collection together, refer to the [Getting Started](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/blob/master/examples/getting_started) example

## Cluster & App Services On/Off Schedule

You can now set an on-off schedule to turn off clusters during no-traffic hours to save on costs!
The cluster and any linked app service will automatically turn off at the specified time slot for each day 
and automatically turn on after the time interval passes.

To set an on/off schedule on a cluster, use this resource:

```
resource "couchbase-capella_cluster_onoff_schedule" "new_cluster_onoff_schedule" {
  organization_id = var.organization_id
  project_id      = var.project_id
  cluster_id      = var.cluster_id
  timezone        = var.cluster_onoff_schedule.timezone
  days = [
    for day in var.days : {
      state = day.state
      day   = day.day
      from  = day.from
      to    = day.to
    }
  ]
}
```

The schedule for each day can be defined in the vars file as follows:
```
days = [
  {
    day   = "monday"
    state = "custom"
    from = {
      hour   = 12
      minute = 30
    }
    to = {
      hour   = 14
      minute = 30
    }
  },
  {
    day   = "tuesday"
    state = "custom"
    from = {
      hour = 12
    }
    to = {
      hour   = 19
      minute = 30
    }
  },
  {
    day   = "wednesday"
    state = "on"
  },
  {
    day   = "thursday"
    state = "custom"
    from = {
      hour   = 12
      minute = 30
    }
  },
  {
    day   = "friday"
    state = "custom"
    from = {

    }
    to = {
      hour   = 12
      minute = 30
    }
  },
  {
    day   = "saturday"
    state = "custom"
    from = {
      hour   = 12
      minute = 30
    }
    to = {
      hour = 14
    }
  },
  {
    day   = "sunday"
    state = "off"
  }
]
```

Take a look at the examples for [Cluster On/Off Schedule](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/blob/master/examples/cluster_onoff_schedule) to understand how to manage the schedule in detail.

This schedule will also apply for to any app service linked to the cluster.

## Cluster & App Services On/Off On Demand

You can turn on/off a cluster and any linked app service on-demand

To turn off a cluster on demand, use the following resource:
```
resource "couchbase-capella_cluster_onoff_ondemand" "cluster_activation" {
  organization_id = var.organization_id
  project_id      = var.project_id
  cluster_id      = var.cluster_id
  state           = "off"
}
```

To turn on the cluster,  change the `state` field's value to `"on"` in the above resource.

While turning on the cluster, you can opt if the linked app service should be turned on at the same time, or if should it remain off.

You can do this by adding the `turn_on_linked_app_service` field and setting it to true or false.

```
resource "couchbase-capella_cluster_onoff_ondemand" "cluster_activation" {
  organization_id            = var.organization_id
  project_id                 = var.project_id
  cluster_id                 = var.cluster_id
  state                      = "off"
  turn_on_linked_app_service = true
}
```

Turning App Service on or off on demand can also be managed exclusive of cluster as follows:
```
resource "couchbase-capella_app_service_onoff_ondemand" "app_service_activation" {
  organization_id            = var.organization_id
  project_id                 = var.project_id
  cluster_id                 = var.cluster_id
  state                      = "off"
}
```

For more information, follow the [examples for managing on-demand activation of clusters and app services](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/blob/master/examples/onoff_ondemand)

## Importing Sample Buckets

To create a sample bucket and import a sample dataset, you can  use the `sample_bucket` resource as follows:

```
resource "couchbase-capella_sample_bucket" "travel-sample" {
  name            = "travel-sample"
  organization_id = var.organization_id
  project_id      = var.project_id
  cluster_id      = var.cluster_id
}
```

The sample bucket name must be one of ['travel-sample', 'gamesim-sample', 'beer-sample'], if another name is used, the creation will fail.

Also check the examples [here](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/blob/master/examples/sample_bucket) for more details.


### Helpful Links

- [Getting Started with the Terraform Provider](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/blob/master/examples/getting_started)
- [Capella V4 Management APIs](https://docs.couchbase.com/cloud/management-api-reference/index.html)
- [Specific Examples in the examples folder](https://github.com/couchbasecloud/terraform-provider-couchbase-capella/blob/master/examples)